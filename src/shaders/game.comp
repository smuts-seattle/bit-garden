#version 450 

// Concept enumerations
const int Soil = 0;
const int Sunflower = 1;
const int Rose = 2;
const int Dogwood = 3;
const int Elder = 4;
const int Thistle = 5;

struct CellState {
  int concept;
  int blood;
  int joy;
};

struct Mutation {
  uint x;
  uint y;
  int concept;
};

struct ShaderParams {
  uint world_width;
  int flip;
  uint mutations_size;
  Mutation mutations[100];
};

layout(local_size_x = 25, local_size_y = 1, local_size_z = 1) in;

layout(std430, set = 0, binding = 0) buffer Left { 
  CellState left[]; 
};

layout(std430, set = 0, binding = 1) buffer Right { 
  CellState right[]; 
};

layout(std430, set = 0, binding = 2) buffer Params { 
  ShaderParams params; 
};

#define check_nearby(_x, _y, _radius_outer, _radius_inner, _check) \
for (int i = 0 - _radius_outer; i <= _radius_outer; i++) {\
  for (int j = 0 - _radius_outer; j <= _radius_outer; j++) {\
    if (!(abs(i) < _radius_inner && abs(j) < _radius_inner)) {\
      int peek_x = int(_x) + i;\
      int peek_y = int(_y) + j;\
      if (peek_x >= 0 && peek_y >= 0 && peek_x < world_width && peek_y < world_width) {\
        CellState cell;\
        if (params.flip != 0) {\
          cell = right[peek_x + (peek_y * world_width)];\
        }\
        else {\
          cell = left[peek_x + (peek_y * world_width)];\
        }\
        _check\
      }\
    }\
  }\
}

void to_dust(inout CellState square) {
  square.concept = Soil;
  square.blood = 0;
  square.joy = 0;
}

void main() {
  uint world_width = params.world_width;
  uint idx = gl_GlobalInvocationID.x;

  uint x = idx % world_width;
  uint y = idx / world_width;
  CellState square;
  if (params.flip != 0) {
    square = right[idx];
  }
  else {
    square = left[idx];
  }

  for (int i = 0; i < params.mutations_size; i++) {
    if (params.mutations[i].x == x && params.mutations[i].y == y) {
      square.concept = params.mutations[i].concept;
      if (params.flip != 0) {
        left[idx] = square;
      }
      else {
        right[idx] = square;
      }
      return;
    }
  }

  if (square.concept == Soil || square.concept == Sunflower) {
    uint count = 0;
    uint revelry = 0;
    check_nearby(x, y, 1, 1, {
      if (cell.concept == Sunflower) {
        count += 1;
        revelry += cell.joy;
      }
    });
    
    if (square.concept == Sunflower) {
      square.joy += int(revelry / 8);
      if (square.joy > 100) {
        square.joy = 100;
      }
      if (count > 3 || count < 2) {
        // Death
        bool loved = false;
        check_nearby(x, y, 2, 2, {
          if (cell.concept == Rose) {
            loved = true;
            i = j = 1000;
            break;
          }
        });

        if (loved) {
          square.blood += 1;
        } else if (square.blood > 8) {
          square.concept = Rose;
        } else {
          to_dust(square);
        }
      } else {
        square.joy += 1;
        if (square.joy > 100) {
          square.joy = 100;
        }
      }
    } else if (square.concept == Soil) {
      if (count == 3) {
        // Life
        square.concept = Sunflower;
      } else if (count > 7) {
        // Love
        square.concept = Rose;
        square.joy = 0;
      }
    }
  } else if (square.concept == Rose) {
    int suffering = 0;
    check_nearby(x, y, 2, 1, {
      suffering += cell.blood;
    });

    if (suffering >= 12) {
      // Sacrifice
      square.concept = Dogwood;
      square.blood -= 60;
    }
  } else if (square.concept == Dogwood) {
    int law = 0;
    bool order = false;
    check_nearby(x, y, 15, 1, {
      if (cell.concept == Dogwood) {
        law += 1;
      } else if (cell.concept == Elder || cell.concept == Thistle) {
        order = true;
      }
    });
    square.blood += 1;
    if (square.blood >= 0) {
      if (law > 10 && !order) {
        // Enthrone
        square.concept = Elder;
        square.joy = 0;
        square.blood = 0;
      } else {
        // Forgotten
        to_dust(square);
      }
    } else if (square.blood < -30) {
      bool zeal = false;
      check_nearby(x, y, 15, 12, {
        if (cell.concept == Elder) {
          zeal = true;
        }
      });
      if (zeal) {
        square.concept = Thistle;
        square.joy = 0;
        square.blood = 0;
      }
    }
  }
    
  if (params.flip != 0) {
    left[idx] = square;
  }
  else {
    right[idx] = square;
  }
}
